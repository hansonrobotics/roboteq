SCRIPT_VER=29
MAX_CHANNELS=2
Dim last_mode[2] as Integer

' Mode values are:
'  -1 stopped
'  0 speed mode
'  1 position mode
' These correspond to the constants in roboteq_msgs/Command. The motor begins in stopped
' mode, the setup for which is run when last_mode[0] != VAR[1].
last_mode[0] = -127
setcommand(_VAR, 1, -1)

' Motor controller configuration. Much of this is particular to SDC2160
' and can be generalized/parameterized as the need arises. 
' https://www.roboteq.com/index.php/docman/motor-controllers-documents-and-files/documentation/datasheets/sdc21xx-datasheet/63-sdc21xx-datasheet/file
' Commands are referenced in this document:
'https://www.roboteq.com/index.php/docman/motor-controllers-documents-and-files/documentation/user-manual/272-roboteq-controllers-user-manual-v17/file

setconfig(_ECHOF, 1) ' Serial echo off
setconfig(_RWD, 1500) ' Watchdog timeout, milliseconds
setconfig(_AMOD, 1, 1) ' Analog input 1 absolute mode
setconfig(_AMOD, 2, 1) ' Analog input 2 absolute mode

' Undervoltage/overvoltage limits 
setconfig(_OVL, 620)
setconfig(_UVL, 100)

for ch = 1 andwhile ch <= MAX_CHANNELS evaluate ch++
	setconfig(_EMOD, ch, 50)  ' encoder usage 
	setconfig(_EPPR, ch, 1024) ' Encoder Pulses per revolution
	setconfig(_MXRPM, ch, 3500) ' Maximal revolutions per minute
	setconfig(_CLERD, ch, 0) ' Close loop error detection 
  ' (e.g. 2 means more than 500ms of command feedback difference of 250)

	' Current limit 
	setconfig(_ALIM, ch, 200) ' Maximum continuous current
	setconfig(_ATGA, ch, 1) ' Amps trigger action 
	setconfig(_ATGD, ch, 30000) ' Amps trigger delay
	setconfig(_ATRIG, ch, 160) ' Amps trigger level 

	' Max acceleration/deceleration 
	setconfig(_MAC, ch, 20000)
	setconfig(_MDEC, ch, 20000)
next

top:
	for ch = 1 andwhile ch <= MAX_CHANNELS evaluate ch++
		if last_mode[ch - 1] <> getvalue(_VAR, ch) then
			last_mode[ch - 1] = getvalue(_VAR, ch)

			' Motor to be stopped (high-Z).
			if getvalue(_VAR, ch) = -1 then
				setconfig(_MMOD, ch, 1) ' Operating mode 
				setconfig(_KP, ch, 0) ' PID proportional gain
				setconfig(_KI, ch, 0) ' PID integral gain
				setconfig(_KD, ch, 0) ' PID differential gain
				setconfig(_ICAP, ch, 0) ' PID integral gap
				setcommand(_G, ch, 0) ' Set speed or relative position
			end if

			' Motor in velocity-control mode.
			if getvalue(_VAR, ch) = 0 then
				setconfig(_MMOD, ch, 1)
				setconfig(_KP, ch, 20)
				setconfig(_KI, ch, 40)
				setconfig(_KD, ch, 10)
				setconfig(_ICAP, ch, 100)
			end if

			' Motor in position-control mode.
			if getvalue(_VAR, ch) = 1 then
				setconfig(_MMOD, ch, 3)
				setconfig(_KP, ch, 2)
				setconfig(_KI, ch, 0)
				setconfig(_KD, ch, 0)
				setconfig(_ICAP, ch, 0)
			end if
		end if
		
		' Send feedback for this channel at 25Hz.
		GoSub PrintFeedback
	next

	' Send status messages for the controller at 5Hz.
	c++
	if c = 5 then
		GoSub PrintStatus
		c = 0
	end if

	wait(19)
	goto top

PrintFeedback:
	print("&f:", ch)
	print(":", getvalue(_MOTAMPS, ch))
	print(":", getvalue(_MOTCMD, ch))
	print(":", getvalue(_MOTPWR, ch))
	print(":", getvalue(_ABSPEED, ch))
	print(":", getvalue(_ABCNTR, ch))
	print(":", getvalue(_VOLTS, 2))
	print(":", getvalue(_BATAMPS, ch))
	print(":", getvalue(_ANAIN, ch))
	print(":", getvalue(_TEMP, ch+1))
	print("\r")
	Return
	
PrintStatus:
	print("&s:", SCRIPT_VER)
	print(":", getvalue(_FLTFLAG))
	print(":", getvalue(_STFLAG))
	print(":", getvalue(_VOLTS, 1))
	print(":", getvalue(_VOLTS, 3))
	print(":", getvalue(_TEMP, 1))
	print("\r")
	Return
